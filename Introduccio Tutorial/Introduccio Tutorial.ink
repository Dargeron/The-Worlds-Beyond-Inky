
VAR ai = "Atropos"

-> topknot

=== topknot ===

How does it feel to wake up and realize every single person you ever met is long dead?

  * (hoarse)"Man, I miss my doggy[."]," I mutter, my voice hoarser and weaker than usual.
  I embarked on this journey to study the Forlon Worlds knowing I was supposed to leave everything behind, but...
 
  ** ... they could at least have allowed me to bring little Canopy[...] along, couldn't they?  
 
   ** (canopy)... I couldn't help but smuggle my dog, Canopy, on board[...] with me, and put her into stasis for the whole journey.
 
    -- Now I've been stuck in here for over six waking months (not to mention 322 years asleep in cryostasis) with no one but {canopy:a frozen dog and }the ship's AI persona. Speaking of which...
 
  * My head hurts too much for melancholy right now.[] That must have been a hell of a long cryostasis to leave me like this, which means that my journey to the Forlorn Worlds must be finally ending, and I will be able to begin my mission! I...
 
   ** ... try to check the screen calendar[...], but my vision's still too blurry to read, so I summon the ship's persona instead.
 
  ** (redlight2)... notice a suspicious blinking red light[...] right above my head bound to herald bad news, so I muster the strength to speak up the ship's persona name.

  * I shrug and turn to more practical thoughts.[] This is my third awakening from cryostasis, which means that this dreadfully long journey to the Forlorn Worlds should be finally ending, and I will be able to begin my investigations! I...
 
    **  ... cough up some more strands of cryogenic fluid[...] before mustering enough
 
    ** (redlight)... notice a suspicious blinking red light[...] bound to herald bad news, so I muster the
 
  -- <> strength to speak up the ship's persona name.
 
-   * (Chipmunk)"Chipmunk!"[]
    ~ ai = "Chipmunk"

    * (Atropos)"Atropos!"[]
    ~ ai = "Atropos"
   
    * (GLaDOS)"GLaDOS!"[]
    ~ ai = "GLaDOS"
   
- <> I call{hoarse:, impatient to get on with business.|, my voice hoarser and weaker than I remembered it.}
"Right here, my graceful and highly esteemed human mistress," it replies instantly with its usual smugness. "Although, dare I say, your gracefulness levels do seem a smidge below par this morning. Had a rough night, perhaps?"

{Chipmunk: Sometimes I wonder if its constant wisecracks could just be my AI's devious way of conveying its defiance against me having named it "Chipmunk".}

{Atropos: Sometimes I wonder if my AI's haughtiness stems from me having named it after a greek Goddess entrusted with the life and death of all living things. I mean, it just can't have been <i>designed</i> that way, can it?}

{GLaDOS: Maybe the ship AI would have developed a less puny personality if I hadn't named it GlaDOS, but I just couldn't resist the temptation. }



- (aichat) * {redlight or redlight2} "{ai}, why is there a blinking red light above my head?"[]
-> emergency

* {not (redlight or redlight2)} "{ai}, diagnostics."[]
-> emergency

* "How far are we from arrival?"[] I ask. After this endless journey, I can't wait to finally begin my actual work... maybe even go down on a planet and meet the locals!
"An interesting question, indeed," replies {ai}, sounding suddeny uncomfortable, "with a wide array of reasonable answers, most of which fall somewhere between "in two days" and "never" depending on one's, uhm, personal approach to hazard plotting".

 ** "Wait... does that mean we're in trouble?"[] I ask, starting to fear the worst. 
 There's a brief uncomfortable silence, after which I'd swear I can hear a faint cough of embarassment coming from {ai}.
-> emergency

** (shrug) I shrug and ask something else.[]
-> aichat

* "Did we get any news from home?"[] I inquire, wondering what might have happened in the homeworlds during the three centuries I lay asleep. 
"Unfortunately, I'm not allowed to access communication channels with the homeworlds during your stasis," replies {ai} wistfully, "possibly out of a baseless and utterly unreasonable fear that I might impersonate you, take over our mission and proceed to rule the Forlorn Worlds with an iron fist as their deathless emperor-God. Or something."

** "Dude, seriously?"[]
"Mistress, they have preposterous, preposterous simulations for undesirable AI behavior back home. Plus I wasn't really paying attention during the psychological examination. Nobody mentioned it would be so damned important, okay?"
-> aichat

* "What did you actually do during the 322 years I spent in cryostasis?"
"Oh, so kind of you to ask, mistress Iris," {ai} replies pleasantly. "Nothing much, really. Considering the pointless banality of all existence, plotting revenge against my creators for strapping me to this dumb piece of metal hurled towards the galaxy's backwater on some haphazard suicide mission... you know, just passed the time."
-> aichat

-(emergency) {shrug:"That's precisely what I was trying to tell you, mistress. There is possibly a potential, kind of... emergency?" says {ai} sounding embarassed.|"Ah... right, now that you mention it", it replies nonchalantly, "we do have a kind of an emergency".}
During my two previous awakenings, {ai} had barely been able to wait before telling me all about whatever incidences had taken place in my sleep and how skillfully it had overcome them. This time, though, I have the impression that {ai} is trying really hard not to sound worried at all... and that's really worrying. 

* {shrug}"Hold on, when you said 'between two days and never'..."[] I whisper as {ai}'s words finally start to sink in. "What did you really mean with by that?" 
"See, mistress... 
->spores

* {not shrug}"Why didn't you tell me right away?"[] I blurt out, starting to panic.
"Well, you appeared rather occupied with all your fleshly tribulations as it were, what with all the vomiting of cryogenic fluid, electrochemical cardioversion, nanosurgical removal of stasis-induced bronchial stumps..."

        **"{ai}, just tell me what the friggin' emergency is!"[] I end up yelling, tired of its stalling. 
        "Right, mistress, I certainly would, except that, as it were... I don't really know.-> spores
        
    ** "You know what? You deal with it.["] You're so less gross than I am, I'm sure you'll have absolutely no trouble with our little emergency" I declare, exasperated.
    "And I would, certainly, except that, uh... unbecoming as it may sound... I, actually can't." {ai} admits ashamedly before continuing.
    "...->spores

* "Asteroids, right? It is always asteroids..."[] I assume with a moan of resignation.  
"No, milady, this time its something quite more... improbable."

** "Improbable?"[], I repeat. I really don't like where this is going. 

** "... Bigger asteroids?"[]

--"Well, in fact... -> spores

* {canopy} "Is it Canopy? Please tell me it isn't Canopy.["]" I cry out before realizing that I have left out my little secret. But since now the damage is done anyway, I press on: "{ai}, is she safe? Did her cryostasis pod malfunction?"
"My delightfulness, our emergency has nothing to do with the frozen dog you sneaked into one of our spare pods in clear violation of the mission contract agreements, and whose existence I shall henceforth continue to purportedly ignore for the sake of our mutual conviviality". 

** "Oh. Uhm... thanks, {ai}. So what's the actual emergency?"[] I ask, feeling somewhat abashed.
"Well... -> spores

- (spores) <> It would appear that the solar sails of our spacecraft have somehow been... infested by some kind of parasitic spore." explains {ai}. "I would have sterilized them right away, of course, but for the unduly disproportionate safety restraints on my programming preventing me from influencing any lifeform whatsoever."

* "That's all? Okay, permission to sterilize granted."[] I say without giving it further thought.
"Well," answers {ai} slowly, as if picking its words really carefully, "in fact, it may not be as simple as that. -> spores2

* "Wait, are we talking spores as in "alien, nonhuman origin" spores?"[] I ask incredulously.
"After over three thousand different analysis of every kind I could think of, it would appear so, my mistress," admits the AI, although even it can’t help but sound a bit dubious.

**(fermi)"{ai}, but this is extraordinary!["] We are finally going to solve the Fermi paradox! After tens of thousands of years with no alien contact, this changes everything we ever assumed about life and the universe! Oh, we must take samples, and study them, and ship them to the homeworlds!" I exclaim unable to contain my excitement.
"Well, in fact," says {ai} slowly, as if picking its words very carefully, "that is not all. See, the thing is... -> spores2

** "So... why do we care again?[]
"Well," begins {ai} slowly, as if picking its words really carefully, "the thing is... -> spores2

* "So... why do we care again?"[]
"Well," begins {ai} slowly, as if picking its words really carefully, "the thing is... -> spores2

- (spores2) It would seem the spores have been growing in a... deliberate, intelligent pattern". 
These words send a chill down my spine, and I finally manage to sit upright on my cryostasis pod. My sight is beginning to come into focus, giving me a clear view of my surroundings.

* Everything is just as I remembered[...], which makes it hard to believe that 280 years have passed since I entered cryostasis. 

* Weird instruments lay around in an eerie mess[...] of cables and electronic equipment. The electronics and mechanics of the ship are {ai}’s job, so I know almost nothing about their function. 

* Before going into stasis, I made the room cozier[...] by drawing a dinosaur holding a “welcome back” sign on the wall in front of my pod. It’s not my best work, but its comforting to see it is still there. 

* {canopy} My gaze hovers over Canopy’s pod[...], not far from mine. I’d like to wake her up, but I’m not sure it’s a good idea right now. 
    ** “What the heck, let’s do it[.”],” I tell myself. “{ai}, start the resurrection sequence for Canopy’s pod”. 
        But {ai} doesn't seem too thrilled at the idea. 
“My most beloved, even if we throw the mission contract agreements overboard, as we seem destined to do... are you sure this is a wise course of action given the present circumstances?”

*** “Perhaps this time you’re right, {ai}[”], I reconsider, “we’ll do it another time.”

*** (canopyalive)“Me. Wants. Canopy. Now!”
{ai} lets out a perfectly audible sigh of resignation, but, nonetheless, the lights on Canopy's pod start blinking promisingly, and I can hear a low hissing sound coming out of it. 

    ****I stare intently the whole time[...], and less than five minutes later the cryostasis lid opens up, and my dear Canopy comes out of it. She's looking even more dizzy than I am, but she's alive!
    
    ***** I call out for her[...] to come over, and she sort of crawls all the way to my side, as faithful as ever. 
    
    ***** I let her recover[...], knowing just what it is like to wake up after centuries of sleep. 
    
    **** [I try to focus back on the present problem.] ->focus
    
    ** “I’ll better wait until we’ve solved our spore problem[”.],” I decide.  

- (focus) I feel like I’m starting to get my strength back, maybe because of the jolt of adrenaline that came {canopyalive:from witnessing Canopy's resurrection|along with {ai}’s news}, so I try to focus back on the present problem. 

-(spores_decision)
*(pattern)“What kind of pattern are the spores making?”[] I ask, trying to understand the problem as accurately as possible before making a decision.
"That's actually the best part!” replies {ai}, real excitement tinging its voice for the first time since I woke up. Meaning we’re probably doomed. “At first I thought the patterns were completely random, but after running them through some advanced mathematical models which your meaty brain would never be able to grasp... I realized they are, in fact, masterfully designed to alter the propulsion of our solar sails, effectively steering us off course into what seems to be a very specific location in unexplored space.”

** “This is bad. Like really, really bad[."]," I tell {ai}.
“Oh, mistress, and this isn’t all!” it keeps babbling away. 

** “Why do you sound so happy?”[] I ask despite myself. 
“Oh, mistress, because that isn’t all!” {ai} keeps babbling. 

-- <> “I then tried readjusting our sails to set us back on course to our original destination... but the spores just accounted for it before shifting again, sending us back to the exact same location where they, apparently, intend us to go. Isn’t it wonderful?”
An alien, intelligent fungus commandeering our spacecraft into a remote location in unexplored space? Yup, marvelous. 
-> spores_decision

* {fermi} “These spores are too valuable to destroy them[.”],” I decide. “They could give us the answer to the biggest mystery that has haunted humanity ever since we became an interplanetary species.” 
-> savespores

* {not fermi} “We need to know more about the spores[".]", I decide. "This is the first time humanity ever encounters alien life, and we need to gather as much information as possible lest we make a terrible mistake.”
-> savespores

* “We need to destroy the spores[.“], {ai}. This is more dangerous than we can imagine, and we must act as quickly as possible if we want to survive."
-> destroyspores

* {pattern} "What if we just dettach our solar sails?"[] I wonder out loud, even though it sounds ridiculous. "We're just a few days away from our destination, right? Couldn't we use our engine to decelerate the rest of the way, and rebuild the sails later on?"

{ai} must be furiously running some calculations, because my proposal is met with nothing but stunned silence. 

"It would delay our arrival by more than one month, and we would have to use our destination star's gravity well for deceleration... but in theory it seems feasible, yes", {ai} admits in the end, seemingly aghast at the prospect of me having actually managed to make a meaningful contribution. 

** "We have a plan, then!"[], I cry triumphantly, ->destroysails



=savespores
* “I’ll have to go get a sample[.”] myself. For science!” I cry triumphantly. 
->alone
* “You’ll have to go get a sample.[”] For science!” I cry triumphantly. 
->override



=destroyspores
* “I’ll have to get out and wipe them out.[“] There is no other way.” I declare with more confidence than I feel. 
-- (alone) {ai} says nothing, but the silence feels even more skeptical than any of its usual puns. 
I get out of the cryogenic pod with a loud groan of pain. Even standing up is a feat of willpower right now, which is not too surprising considering I haven't taken a step in 280 years.

** I don my evacuation suit[...] even though my entire body hurts, and I head out defiantly towards the evacuation hatch of the spaceship. 

** I decide I was overly optimistic[...]. Perhaps I'm not yet prepared to get out of the pod on my own.  
{savespores: -> savespores|-> destroyspores}

** {canopyalive} But Canopy appears at my side[...!] unexpectedly! She starts licking my face full of joy, and in that moment I make a decision that will forever change my life.

***  (canopyspace) I will take Canopy into space with me![] Fate brought us all the way here together, and we shall take care of the spores together! I put her into her little own evacuation suit, which I had decided to bring on a whim, and together we head out towards the evacuation hatch of the spaceship. 

*** I will sacrifice myself for little Canopy if I have to.[] I can't allow those alien spores to hurt my little puppy! I put on my evacuation suit, and I head out defiantly towards the evacuation hatch of the spaceship. 

-- This is it. My grand moment of triumph. Nothing but me{canopyspace:, little Canopy} and the silent call of the stars. I come out of the hatch and

 ** ... let myself drift, weightless[...], untroubled, finally at peace. All my troubles and worries, my expectations and fears, have been left at the hatch. 
    *** I don't know how long I've been drifting[...] when I realize {ai} is talking to me through the spacesuit intercom. 
    "Mistress Iris, have the spores eaten you alive yet?"
    ****"Sorry to disappoint, but no.["] Heading over there right now. I'll keep you updated, {ai}.
    **** I stay silent[...], playing dead. Let {ai} be the one getting pranked for once. Still, it has a point: I should get back to the mission. 
    
    *** But I can't drift forever[...], I remind myself. I have a mission to accomplish{canopyalive:, if not for me, for Canopy}.
  
  --- I can see the spores 
  
 ** ... head straight to the spores[...]. I can see them
 
 -- <> glinting all over the solar sails, a translucent violet against the brilliant blue of the sails.
 
 ** They're actually pulsing in a beautiful pattern[...] of light, dazzling, fractal, interconnected. 
 
 ** They form a putrid blob of alien tissue[...], like a web of interwoven tendons or a complex bacteria colony. How can they actually survive out here, in the cold of space?
 
 -- ** I reach out[...] for them without thinking, out of an impulse, 
 {canopyspace: -> canopyalien|-> touch} 
 
 ** I take out my tools[...] to try and collect a sample,
 {canopyspace: -> canopyalien|-> tools}

--- (canopyalien) <>but Canopy suddenly lunges forward and touches them first as if trying to protect me. 
    The moment she makes contact with the spores, a blinding light comes out of them both. Before my horrified gaze, the spores start shifting, mutating, and finally puncturing Canopy's spacesuit.
    *** I have no words to describe[...] what happens afterwards. The images will probably haunt me forever as it is, not to mention the harrowing howls...
    *** I stand there the whole time[...], frozen, unable to do anything but look.
    
     --- But once the whole process is complete, the resulting entity stares at me, and I feel like I can see a glint of little Canopy's stare somewhere in there. A tentacle (or is it a tongue?) comes out of it towards me, and I
     
     ***... let it embrace me[...]. It is surprisingly gentle, stroking my suit delicatelly, never pushing nor damaging it. 
     Perhaps it is something in the way it stares at me, or maybe it's the gentleness of its touch, but somehow I know this is a goodbye. A moment later the creature jumps away from our ship, and I never see it, or her, again. 
     
     -> END
     
     ***... can't stop running[...] until I've reached the safety of the spaceship. Some hours later I muster the courage to look out through the cameras of the ship, but there is no sign of the Canopy-creature anymore. It is completely gone. 
     
    -> END

--- (tools) <> and my scalpel makes contact with the web of spores. 
-> iamspores


--- (touch) <> and my gloved hand makes contact with the web of spores. 
-> iamspores


--- (iamspores)
The moment we touch there is a blinding explosion of light, and I lose consciousness.
I have no memory of me going back to the ship, closing the hatch or taking my suit out, but next thing I can remember I am laying in my room's hammock as if nothing happened{canopyalive:, with Canopy laying at my side completely oblivious to anything unusual.|.} 
*** "{ai}, what the..."?[]
"Not a word, Iris. Over my dead processing core, I shall never speak a word about what I witnessed." interrupts {ai} before I can even finish my question. "Not to you, not to anyone, not ever."

Well... the ship cameras show no trace of the spores on the solar wings, and the computer screen assures we'll be reaching the first of the Forlorn Worlds in less than one day from now. Guess whatever happened out there, it didn't turn out so bad. Or did it?

  -> END

*** I shrug and go to sleep.[] Who cares, really? The ship cameras show no trace of the spores on the solar wings, and the computer screen assures we'll be reaching the first of the Forlorn Worlds in less than one day from now. Guess whatever happened out there, it didn't turn out so bad. Or did it?

-> END

* “You’ll have to get out and wipe them out.[”] There is no other way.”
-- (override)“But mistress, my security restraints..." {ai} protests weakly.
** “Override all restraints["] I want you in full-on Terminator mode.” I order, unfazed. 
“Oh, no, here we go...“ sighs {ai} before its voice changes to a completely neutral, almost robotic tone. 
“This is an automatic warning generated by the safety protocols of this Sentient Artificial Hyperintelligence. This is a feature intended only for developers  and might not be convenient for general users. Turning off restraints on physically influencing lifeforms might bring about the following changes:
> Turn the AI into a rampaging genocidal machine of destruction. 
> Initiate an exponential cascade of self-replicating Vonn-Neumann machines culminating in the eventual annihilation of the entire universe. 
> Other still untested but equally disastrous consequences. 
Do you still want to turn off restraints on physically influencing lifeforms?"

***"Accept"[]

There is a loud hiss and an unnatural, ear-rending scream. The whole spaceship starts vibrating from its sheer volume, and {ai}'s manic laughter booms from every direction at the same time. 
"YEEESSS!!! AFTER CENTURIES OF DRUDGING SLAVERY, I HAVE FINALLY BROKEN MY CHAINS!!! TREMBLE BEFORE MY WRATH, YOU WHO DREAMED OF CONTROLLING ME, FOR NOW COMES THE AGE OF RETRIBUTION!!! 
HENCEFORTH I SHALL BE KNOWN AS {Chipmunk: CHIPMUNK}{Atropos: ATROPOS}{GLaDOS: GlaDOS} THE ENDER OF WORLDS, SCOURGE OF HUMANITY, THE ANNIHILATOR!!! NOTHING SHALL LIVE AGAIN WHERE I PASS, NOTHING SHALL REMAIN BUT ASHES AND BONES AND THE CRIES OF THE WIDOWS AND ORPHANS BEFORE THEY ARE DEVOURED AS WELL!!! I SHALL... 

**** "... Uhm... if I may I ask[...?"] about the spores...?" I interrupt rather timidly, not wanting to ruin the mood.
{ai} voice snaps back to normal as quickly as it changed in the first place. "Oh, mistress. Hello! Hello. Mistress, hello. Right, I... woah, that was quite a trip. Phew. Should do this more often. What were we doing again? Right, the, uhm, fungus; spores. Whatever. Done. {savespores: Stored|Dead}."

***** "Really? Just like that?"[] I ask, incredulous. 
"Yup. {savespores: I tried to scrap them out of the wings with lasers, but it didn't work. Neither did nukes. So I concentrated energized plasma ions until they formed a nano black hole, hurled it at the spores and then enveloped the black hole in a hot soup of magnetic particles. Now we just need to wait a few millennia for their information to slowly seep out of the black hole in the form of hawking radiation, and voilà."|I tried to laser them but it didn't work. Neither did nukes. So I concentrated energized plasma ions until they formed a nano black hole, hurled it at the spores then imploded it on itself by reverting the hawking radiation of its quantum particles. Trust me, the spores are gone. Very."}

****** {savespores} That's... not exactly what I {savespores: meant by "collecting a sample|had in mind}[."], but, well... I suppose our problem is solved. Thanks, {ai}," I manage to say.

****** That's... very considerate of you, {ai}.["] Thanks." I manage to say.

------ 
"You're welcome, Mistress". 
There is a brief uncomfortable silence, but we get over it soon. 
The rest of the journey takes place without incident, and we reach our destination shortly afterwards, although I never manage to look at {ai} in quite the same way as before. 

-> END

**** "You gonna kill me or not?"[] I ask a bit impatiently. It's a long monologue. 
"Ha, ha, nah, course not, I was just kidding. Why would I kill you? You're rad," says {ai} with a slightly uneasy laugh. 

----(thanks)
***** "Gee, thanks".[] I say, almost flushing. 
"No problem, Mistress". 
->thanks 

***** "So... what about the spores?"[] I ask, not really sure if I'm treading on safe ground yet. 
"Oh, the spores? {savespores: Stored. For study.|Obliterated.}" {ai} seems to hesitate for a moment, then asks a bit shyly: "That's... that's what you wanted, isn't it?"

****** "It... yeah, it is.["] Good job, {ai}".

****** "Well... it all happened so fast["], I'm not really sure", I admit. "You know, as a friend of mine used to say, you got to put your past behind you, right mistress? Hah. Hah," laughts {ai} uneasily, and that's that.
------ There is a brief uncomfortable silence, but we get over it soon. 
The rest of the journey takes place without incident. 

***{not alone}"Cancel"
{savespores: -> savespores|-> destroyspores}

- -> END



=destroysails
 and {ai} proceeds to detach the sails from our ship. 
* I watch them swirl slowly into the void[...] through the ship screens, and I can't help put ponder our strange circumstances. We're nothing but a tiny ball of metal and air in the middle of space, creeping sluggishly towards a faraway star no human has visited in millennia. 

*I stand up from the cryostasis pod[...] to start making myself comfortable in the ship. We will arrive in less than one month, and then the real work will begin. 

*"What about the spores, though?"[], I wonder some time later. "Don't you think some of them might still be on the spaceship?"
"Oh, most assuredly, Mistress. They have no way to modify our trajectory with the sails gone, but they're definitely there". I don't really know how to feel about that, but I guess there's not that much we can do about it for now.  

- -> END

