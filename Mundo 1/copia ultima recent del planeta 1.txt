VAR ai= "Atropos"
VAR pet=""
VAR day=1
VAR shelter=""

-> LLEGADA

=== LLEGADA

Mi primera vivencia nada más pisar el planeta de Aang, tras más de medio año encerrada a bordo de una nave diminuta, ha sido un explosivo ataque de vómito. La culpa la tuvo, sin duda,

* ... la desorientadora mezcla de olores y colores[...] del planeta. Su densa vegetación azulada emitía una potente fragancia
** ... dulzona[...]
** ... acre[...]
** ... a huevo podrido[...] 
-- <> que lo permeaba todo y que, sumada a la multitud de formas y colores propios de la selva, tuvo el efecto inmediato de que la cabeza me empezase a dar vueltas incontrolablemente. 

* ... el desplazador cuántico[...] de la nave, que me permitió llegar al planeta en cuestión de segundos y me recogerá llegado el momento.  

** No entendí ni una palabra[...] de la explicación del técnico de la Agencia que trató de hacerme entender su funcionamiento: algo sobre funciones de onda, decoherencia cuántica y conos de vacío. En fin, lo importante es que estoy aquí de una pieza. 

** Nunca lo había utilizado[...] anteriormente, ni en la nave ni en mi mundo natal, y lo cierto es que es una experiencia

*** ... cuanto menos desconcertante.[] Si tuviera que describirlo con palabras, diría que fue como un viaje de ayahuasca pero condensado en quince segundos, profudamente borracha y colgando cabeza abajo. 
*** ... que no relataré aqui.[]

*... la cena de ayer[...]: {ai} insistió en prepararme una delicatessen local con un portentoso olor a calcetines usados que me tuvo toda la noche dando vueltas en mi hamaca con retortijones en el estómago. 

** Sospecho que fue otra de sus trastadas[...], como la vez en que me hizo creer que había habido un fallo en el motor de fusión y teníamos que recolectar isótopos de deuterio de un asteroide cercano para repararlo. No fue hasta que ya me encontraba fuera de la nave tratando de aclararme con los propulsores del traje de evacuación que empecé a escuchar sus carcajadas por el canal de radio. 
Esta vez juro que a mi retorno a la nave me vengaré como es debido: le... 

*** ... desactivaré los sensores electromagnéticos[...] durante una semana, cosa que por alguna razón incomprensible le incomoda profundamente.  

*** ... volveré a reconfigurar el arquetipo linguístico[...] a "capitán pirata del siglo XVI". Aún se me saltan las lágrimas cuando me acuerdo de la última vez. #poner chiste?

*** ... obligaré a ver episodios de "Friends" en bucle[...] durante tres días. Con su velocidad de procesamiento, calculo que esto equivaldrá a unos cuatro trillones de visionadas de la serie completa. Quiero saborear sus lágrimas. 

**Si esto es lo que comen los nativos[...], ya puedo empezar a prepararme para desarrollar una larga e íntima relación con los problemas digestivos en este viaje. 

- En cuando recuperé la suficiente compostura para pensar en algo más elaborado que en no regurgitar mis propios intestinos, 

- (inicio) * ... me dispuse a examinar los alrededores[...] de mi lugar de aterrizaje en busca de señales de vida inteligente. ->buscarvida

* ... empecé a preparar un refugio[...] improvisado tal como había practicado innumerables veces en mis entrenamientos. ->refugio

* ... levanté la mirada y me encontré cara a cara[...] con una criatura de lo más peculiar observándome. 
->criatura





=buscarvida
Según los cálculos de {ai}, el desplazador cuántico debía haberme dejado lo bastante cerca de una población humana para caminar hasta allí, pero lo bastante lejos para que mi repentina aparición pasase desapercibida.

* No muy lejos había un promontorio[...] des del que sin duda alcanzaría un mayor rango de visión, y me propuse escalarlo. Volver a correr y trepar por un espacio abierto era una sensación irresistible,

** (calma)... {gritar: aunque|pero} me tomé la subida con calma[...], consciente de que aún no había recuperado todas mis fuerzas tras el largo viaje a los Mundos Abandonados, y que cualquier accidente en aquel planeta desconocido podia ponerme en un grave peligro.

*** Llegué a lo alto del promontorio[...]->promontorio

***{not criatura} Me di cuenta de que alguien me seguía[...] hacia la mitad del camino, en una curva pronunciada de la subida. Solo me dio tiempo a vislumbrar una silueta por el rabillo del ojo, pero desde aquel momento empecé a prestar más atención, y de vez en cuando me parecía escuchar ruidos de movimiento entre los matojos. 

Al final, cansada de andar vigilando mis espaldas,

**** ... preparé una emboscada[...], escondiéndome en el interior de una grieta de la pared de roca con la esperanza de pillar por sorpresa a mi perseguidor.
Y en efecto, pocos minutos después escuché algo pesado moviéndose entre los matojos frente a la grieta. 

***** (placaje) Me abalancé sobre la fuente de los sonidos[...], pero, para mi sorpresa, no se trataba de ningún humano sinó de una criatura de aspecto inofensivo, de cuello alargado y suave pelaje violáceo, un color parecido al de la tierra y la arcilla de los alrededores.
Mi repentina embestida debió asustarla aún más de lo que su presencia me había asustado a mí, 

****** ... pero conseguí tranquilizarla[...] mediante caricias y sonidos amables. 
Tenía el tamaño de un perro grande, y en ningún momento hizo amago de atacarme o morder, por lo que deduje que se trataba de algún tipo de herbívoro. 
A pesar de nuestro pequeño encontronazo, la extraña criatura...
******* ... no parecía ansiosa por irse[...] a ninguna parte, de modo que

******** (mascota)... dejé que se quedase conmigo[...] y ahora me sigue encantada por todas partes. 
->docil

******** ... tuve que ahuyentarla[...] con gritos y aspavientos exagerados hasta que por fin se marchó.
->docil

******* ... me lamió y olfateó con curiosidad antes de alejarse[...] con aire despreocupado por entre la espesura de la selva. 
->docil


****** ... pues no pude evitar que escapase[...] despavorida entre la vegetación de la selva. 
Llegué a lo alto del promontorio->promontorio

***** Pensé que sería mejor permanecer oculta[...] en la grieta, pues en realidad no tenía por qué estar persiguiéndome un ser humano, y yo no tenía ni idea de la clase de fauna que poblaba el planeta de Aang. 
Quienquiera que fuera el causante de los sonidos, tras unos momentos de búsqueda pareció darse por vencido y prosiguió su camino entre la vegetación de la selva.
-> reanudar


***** (idioma)"Quién anda ahí?", grité[...] en el idioma de los nativos, que {ai} había descifrado tras armonizar la simulación, y yo había descargado en mi implante de memoria. 
Desafortunadamente, el sonido de mi voz pareció surtir el efecto contrario al deseado, pues mi perseguidor salió disparado hacia la selva sin dejar ningún rastro. 
-> reanudar

**** ... di media vuelta y empecé a gritar[...] en voz alta en su dirección, con la esperanza de que mi perseguidor decidiera revelarse al saberse descubierto.

***** No hubo suerte[...]; en cuanto me acerqué a la zona de vegetación donde había escuchado ruidos por última vez, algo salió disparado entre los arbustos en dirección opuesta. Traté de distinguir la forma de mi perseguidor, pero la vegetación era demasiado densa para ver nada. -> reanudar

***** Encontré su rastro[...], pero nada más. Las huellas, más pequeñas que las mías pero marcadas claramente en la húmeda arcilla violácea de la montaña, eran de origen claramente no humano, quizás un poco parecidas a las de una cabra. Me pregunto qué aspecto tendrá su misterioso autor...
-> reanudar

**** (despistar)... traté de despistar a mi perseguidor[...] corriendo a toda velocidad cuesta arriba con la intención de dejarlo atrás y que me perdiera el rastro. 

** (atope)... y al poco rato ya estaba corriendo y brincando[...] cuesta arriba como una cabra montesa, {gritar:mi penoso debacle por la selva perfectamente olvidado.|disfrutando de mi recién recuperada libertad y riéndome a carcajadas de puro goce.}

-- *** Llegué a lo alto del promontorio[...] justo cuando los dos soles de Aang se estaban poniendo, tiñendo la selva alienígena de colores verdosos y anaranjados. {despistar: Supuse que había logrado despistar a mi perseguidor, puesto que no volví a ver ni escuchar ninguna señal de su presencia, y allí me quedé;|Allí me quedé,} agotada pero satisfecha, contemplando el atardecer hasta que la última luz de los dos soles dió paso a un brillante cielo nocturno repleto de estrellas. 
Fue entonces cuando reparé -> promontorio.atardecer

*** La pendiente era bastante escarpada[...] y subirla a tanta velocidad no era tarea fácil, especialmente tras meses encerrada entre cuatro paredes, pero llegué a la cima sin nada más que unos cuantos arañazos y magulladuras{despistar:, y ni rastro de mi misterioso perseguidor|}.
Allí me quedé, agotada pero satisfecha, contemplando el atardecer hasta que la última luz de los dos soles dió paso a un brillante cielo nocturno repleto de estrellas. Fue entonces cuando reparé -> promontorio.atardecer

*** Me siento tentada a omitir[...] mi ridícula caída por la pendiente, pero, dado que este es mi diario de campo, debo comprometerme a contar cada detalle de mi historia aún a riesgo de convertirme en el hazmerreír de innumerables estudiantes de antropología del futuro. 
Baste decir que sí, me tropecé; sí, caí rodando por la ladera del promontorio con mi ropa echa girones y mis escasas pertenencias esparcidas por la selva. Pero al menos no me rompí ningún hueso ni me lesioné de forma permanente, que es lo que importa.{despistar:.. por no mencionar que mi astuta maniobra de distracción sirvió para dejar atrás a mi misterioso perseguidor, del que ya no volví a encontrar ni rastro. Justo como lo planeé.} 
-> reanudar


* (gritar)Empecé a gritar a pleno pulmón[...] al tiempo que deambulaba por los alrededores con la esperanza de que me escuchara algún nativo, pero no parecía haber nadie lo bastante cerca. 
Tras varias horas de búsqueda infructuosa

** ... acabé volviendo al punto de partida[...] con las manos vacías y sintiéndome derrotada.

***Traté construir algún refugio[...] redumentario a pesar de que ya casi había anochecido, pues me negaba a dormir al descubierto en media de una selva alienígena. Y debo decir que, dada la precariedad de mis circumstancias, estoy bastante orgullosa del resultado. 
~ day = day + 1
-> DESPERTAR.basebien

***Ya era demasiado tarde para construir un refugio[...], pero la temperatura seguía siendo cálida, de modo que pasé la noche allí mismo, a la intemperie, bajo el cielo repleto de estrellas de aquel lejano mundo alienígena.
~ day = day + 1
-> DESPERTAR.base

** ... me acordé del promontorio elevado[...] que había desestimado por la mañana, pero cuyo atractivo había aumentado significativamente tras varias horas vagando perdida por la selva. 
Como estaba decidida a no desperdiciar el día por completo, me propuse escalar el promontorio costara lo que costara, 
*** [... aunque me tomé la subida con calma...]->calma
*** [... y al poco rato ya estaba corriendo y brincando...]->atope

** (rastro)... encontré un rastro prometedor[...] que se adentraba en la selva para luego subir poco a poco montaña arriba. A pesar de mis sospechas, no tuve ninguna garantía de que fuera de origen humano hasta que encontré lo que parecía ser

*** ... un trozo de flecha[...] rota entre unos matojos del lado del camino. La punta de sílex había sido tallada cuidadosamente, y me la guardé para dibujarla cuando tuviera un momento. 

*** ... un retazo de tela[...] atrapado entre los pinchos de una planta trepadora. No cabía duda de que había sido confeccionado por seres inteligentes, y me la guardé para dibujarla cuando tuviera un momento.  

--- A mitad de camino anocheció, 

*** ... momento en el que di media vuelta[...] sin dudarlo. Si algo tenía claro, era que no podia arriesgarme a perderme de noche en la selva de un planeta alienígena desconocido. 
{refugio:
Al cabo de poco me encontraba en el esponjoso lecho de musgo azulado que había construido por la mañana en el interior de mi fabulosa {shelter}, y, con todo el cansancio acumulado a lo largo del día, no tardé ni diez minutos en encontrarme durmiendo a pierna suelta.
-> DESPERTAR.basebien
-else: 
Regresé al claro donde me había materializado el desplazador cuántico, preparé un refugio improvisado con algunas hojas cercanas, y me quedé dormida al instante tras todo el día trotando arriba y abajo por la selva. 
~ day = day + 1
-> DESPERTAR.base
}

*** ... pero yo seguí adelante[...], impertérrita. Si no podía lidiar con algún pequeño inconveniente, ¿entonces se puede saber qué hacía a solas en medio de la selva de un planeta alienígena en la parte más de remota de la galaxia?
Debía llevar unas de dos horas subiendo montaña arriba cuando distinguí el resplandor de una hoguera lejana, lo cual sólo podía significar una cosa: ¡mi primer contacto con nativos!
-> cabananoche

-------- (reanudar) ******* Llegué a lo alto del promontorio[...] -> promontorio

------ (docil) Su docilidad me hace pensar que debe descender de algun animal doméstico traído por los primeros colonos, que se habrá asilvestrado con el transcurso de los milenios. 
{not mascota: -> reanudar| -> nombremascota}









=nombremascota
 Como es natural, tuve que ponerle un nombre a mi nueva acompañante. "¡Te llamarás... 

    * "Houyhnhnm!"[]
    ~ pet = "Houyhnhnm"

    * "Piecito!"[]
    ~ pet = "Piecito"
   
    * "{ai} Jr.!"[]
    ~ pet = "{ai} Jr!"

- <> proclamé triunfalmente mientras le acariciaba la cabecita. "¿A que te gusta? ¡¿A que sí?!" le pregunté, y ella {criatura:volvió a undular|unduló} su largo cuello, evidentemente encantada. 

{buscarvida: "¡Vamos, {pet}, que aún tenemos mucho camino por delante!" exclamé, y ella empezó a trotar alegremente a mi lado como si me conociera de toda la vida.|"Madre mía...", murmuré, incapaz de evitar que se me dibujara una sonrisa ante el evidente deleite de mi nueva amiga, "¿Pero qué voy a hacer yo contigo, eh?" le pregunté con dulzura.}

* {criatura} -> adoptada

* {buscarvida} Llegué a lo alto del promontorio[...] -> promontorio








=promontorio

 -(noche) <> caída la noche, con la respiración algo agitada por la subida pero sin más percances. 
Era una lástima no haber podido observar el territorio que me rodeaba a la luz del día, pero por suerte no tardé en reparar

-(atardecer) <> en las luces parpadeantes de varias hogueras aglomeradas a unos cuantos quilómetros de distancia, así como en una hoguera solitaria en lo alto de la siguiente montaña, que parecía encontrarse un poco más cerca. 

* Pasé la noche en la cima[...] del promontorio, en una cueva vacía que encontré allí mismo, pues no tenía sentido arriesgarme a bajar todo el camino de noche pudiendo hacerlo a la mañana siguiente. La noche era cálida,

** ... pero de todos modos encendí una hoguera[...] pequeña para alumbrarme y ahuyentar a los animales salvajes. 
Así pasé mi primera noche en el planeta de Aang, alumbrada por mi pequeña hoguera bajo un cielo estrellado{nombremascota:, con mi nueva compañera {pet} acurrucada a mi lado,} y preguntándome si mañana al fin conseguiría hacer contacto con los habitantes del planeta. 
~ day = day + 1
->DESPERTAR.cimabien

** ... por lo que preparé un lecho[...] de azuladas hojas gigantes que recolecté por los alrededores y me tumbé allí mismo, rendida tras toda la actividad del día.
Así pasé mi primera noche en el planeta de Aang, en un camastro de hojas, observando por la entrada de la cueva un cielo tan repleto de estrellas que apenas podía distinguirlas unas de otras{nombremascota:, con mi nueva compañera {pet} acurrucada a mi lado} y preguntándome si mañana al fin conseguiría hacer contacto con los habitantes del planeta. 
~ day = day + 1
->DESPERTAR.cimabien

* {refugio} Regresé al claro para dormir en mi {shelter}[...], naturalmente. Por supuesto que encontrar a los nativos era importante, pero era noche cerrada, y a mi me esperaba el maravillosamente esponjoso lecho de musgo que había pasado más de media hora construyendo en el interior de mi refugio. 

* Me dirigí hacia la hoguera más cercana[...] a pesar de que ya era noche cerrada: había llegado hasta allí arriba con la esperanza de encontrar señales de los nativos, y ahora que tenía pruebas de que había alguien a una hora o dos de distancia no podia echarme atrás. 

** Fue un paseo agradable[...] bajo la luz de las estrellas, con los sonidos de las aves nocturnas acompañándome {nombremascota: y {pet} trotando alegremente a mi lado, adelantándose de vez en cuando para olfatear alguna flor interesante o arrancar de un mordisco alguna hoja particularmente sabrosa.| y el susurro del viento en las hojas por única compañía.}
No había pasado ni media hora cuando vislumbré el brillo de la hoguera que había observado desde la cima del promontorio, esta vez a unos escasos cien metros de distancia. 
-> cabananoche

** Fue una idea terrible[...], como comprobé más tarde: la jungla nocturna era un lugar escalofriante, lleno de ruidos y sombras acechantes, y eso que tenía una idea bastante precisa de hacia dónde me dirigía. {nombremascota: Menos mal que por lo menos contaba con la fiel compañía de {pet}, que seguía trotando alegremente a mi lado sin dar señal de que nuestro entorno la amedrentase en lo más mínimo}.
De un modo u otro di con un rastro que parecía humano y que subía por la montaña en dirección a la hoguera, y, efectivamente, al cabo de poco empecé a vislumbrar de nuevo el brillo del fuego, esta vez a menos de cien metros. 
-> cabananoche

* Emprendí el trayecto hacia el grupo de hogueras[...] lejanas a pesar de que ya había caído la noche, demasiado emocionada ante la posibilidad de establecer contacto con los nativos como para pensar demasiado en las consecuencias... hasta que, dos horas más tarde, no me quedó más remedio que aceptar que me había extraviado por completo. 
Completamente exhausta y desorientada,

** ... traté de regresar a la cima[...] del promontorio siguiendo mi propio rastro a la inversa, cosa que sorprendentemente funcionó, aunque

*** ... me caí en una charca[...] de lodo apestoso que había en el interior de una zanja. Lo cierto es que a aquellas alturas ya solo quería dormir, y apenas me importó, aunque no sabía si los nativos me aceptarían con los brazos abiertos ahora que olía  a mofeta muerta.  
~ day = day + 1
-> DESPERTAR.cimamal

*** ... me atacó una especie de mapache[...] al que debí asustar en su nido, y al que sospecho que solo conseguí sobrevivir gracias a la oportuna aparición de un palo de metro y medio, que blandí aterrorizada a pesar de que el "mapache" medía cinco veces menos que yo. Los mundos alienígenas son aterradores. 
~ day = day + 1
-> DESPERTAR.cimamal

*** ... volví a perderme[...] tres o cuatro veces más. Lección aprendida, Iris: a partir de hoy, se acabó deambular de noche por selvas tenebrosas en planetas alienígenas desconocidos. 
~ day = day + 1
-> DESPERTAR.cimamal

***{nombremascota} ... dudo que sin la infalible orientación de {pet}[...] hubiera llegado muy lejos. Por suerte, mi nueva compañera parecía conocer el camino a la perfección, y cada vez que me veía dudar se adelantaba y undulaba su cuello como apremiándome a seguirla. 
Nada más llegar a la cima me desplomé, agotada, sobre un montón de hojas cercanas, y creo que debí dormirme incluso antes de tocar el suelo. 
~ day = day + 1
->DESPERTAR.cimabien

** ... traté de regresar al punto de aterrizaje[...], que según mis estimaciones no podía quedar muy lejos. No sé ni cómo lo hice, pero en menos de quince minutos aparté unas hojas y di exactamente con el claro en el que 
{refugio: 
me había materializado aquella mañana. Juro que nunca me había alegrado tanto de haber dedicado una mañana entera a acarrear hojas y troncos. En el momento en que vi mi preciosa {shelter}, con su esponjoso lecho de musgo recién recolectado, casi se me saltan las lágrimas. 
~ day = day + 1
-> DESPERTAR.basebien
-else: 
<>me había materializado aquella mañana, donde me preparé un lecho de hojas improvisado y caí rendida, agradecida de poder descansar aunque fuera unas horas.
~ day = day + 1
-> DESPERTAR.base
}
** ... seguí abriéndome camino[...] selva adentro a pesar de encontrarme totalmente extraviada, hasta que finalmente tuve que reconocer que ya no tenía sentido seguir adelante, más que nada porque ya no tenía la menor idea de qué dirección era "adelante". 
Demasiado agotada para seguir pensando después de todo el día de actividad, me envolví en una de las gigantes hojas azuladas que producían las palmeras locales, y caí rendida allí mismo, en medio de la selva, {nombremascota: con la única compañía de {pet}, acurrucada fielmente a mi lado}. 
~ day = day + 1
-> DESPERTAR.selva

- ->END









=cabananoche
*Me acerqué sigilosamente[...] a la hoguera, cuidándome de no hacer ningún ruido, y al cabo de poco escuché una voz humana por primera vez en más de 300 años: estaba cantando. 
{nombremascota:Curiosamente, {pet}, que se había mostrado dócil y despreocupada durante todo el trayecto, ahora se negaba a dar ni un paso más, encogida en el suelo con aspecto asustado. No me quedó más remedio que dejarla atrás, temblorosa, confiando en que seguiría allí a mi regreso.}
Al acercarme a la luz de la hoguera distinguí la silueta de una construcción rudimentaria al lado del fuego. Era una especie de choza, las paredes echas de ramas y barro, y el techo de hojas secas entretejidas. 
Y allí, frente a la hoguera, el cantante: un hombre desnudo de unos cuarenta años
** ... con el pelo largo y trenzado[...] cayéndole por la espalda en un diseño intrincado y meticuloso. ¿Tendría algun significado particular en su cultura?  
** ... de cuerpo atlético y musculoso[...], con manos fuertes y callosas, que en ese momento trenzaban y destrenzaban con dulzura unas briznas de hierba sin interrumpir su canción.  
** ... contemplando las estrellas apaciblemente[...] sin interrumpir en ningún momento su lenta canción sin palabras. 

-- *** Sin querer aplasté una ramita[...], que se rompió con un sonoro crujido delator. El hombre se puso en pie al instante, completamente alerta, agarrando con la mano derecha una piedra cercana, y yo me acerqué hacia la luz de la hoguera con las manos en alto, tratando de no hacer ningún gesto amenazante. 
El hombre pareció relajarse al verme, tal vez porque venía sola, o quizás porque vió que también era humana. Sonriendo tranquilizadoramente, -> contactobien

*** Me acerqué a la hoguera en silencio[...] para no interrumpir su canción. Al verme aparecer en el círculo de luz de la hoguera, el hombre se puso tenso y dejó de cantar, pero enseguida se dió cuenta de que venía sola y pareció relajarse de nuevo.
Sonriendo tranquilizadoramente,

*** (interrumpir) Interrumpí su canción[...] para preguntarle si podía sentarme a su lado en la hoguera, lo cual pareció asustarlo, puesto que se levantó de un salto sujetando una piedra con su mano derecho y observandome con mirada desconfiado y alerta, sin decir nada.  
 Tratando de sonreír tranquilizadoramente,

---(contactobien)*** ... me presenté y le expliqué que estaba perdida[...] con la intención establecer algún tipo de complicidad, pero {interrumpir or desastre:
<>aquello solo pareció enfurecerlo todavía más, hasta el punto que se levantó y, visiblemente alterado, me dirigió la palabra por primera y última vez: 
"¡Vete, demonio, no tienes poder sobre mí!", profirió, casi como un encantamiento. 
No me quedó más remedio que marcharme por donde había venido y pasar la noche en la selva, profundamente confundida. ¿Qué diantres habría hecho mal? ¿Y por qué me llamaría demonio? 
-else: 
<>cuanto más hablaba más parecía incomodar a aquel hombre extraño y solitario.
} 
{interrumpir or desastre: 
~ day = day + 1
}
{interrumpir or desastre: -> DESPERTAR.selvacabanaliada}

*** ... le pregunté cuál era su nombre[...], pero {interrumpir or desastre:
<>aquello solo pareció enfurecerlo todavía más, hasta el punto que se levantó y, visiblemente alterado, me dirigió la palabra por primera y última vez: 
"¡Vete, demonio, no tienes poder sobre mí!", profirió, casi como un encantamiento. 
No me quedó más remedio que marcharme por donde había venido y pasar la noche en la selva, profundamente confundida. ¿Qué diantres habría hecho mal? ¿Y por qué me llamaría demonio? 
-else: 
<>aquello solo pareció incomodarlo. El hombre me miró con una mezcla de horror y desprecio y siguió sin decir palabra, prefiriendo seguir contemplando el fuego en silencio. 
} 
{interrumpir or desastre: 
~ day = day + 1
}
{interrumpir or desastre: -> DESPERTAR.selvacabanaliada}


*** ... me senté en silencio en su hoguera[...] y él, también sin decir palabra, me pasó un cuenco con lo que parecía ser una especie de pasta confeccionada a partir de alguna fruta local. 
Al cabo de un rato {desastre:empezó a cantar suavemente|empezó a cantar la misma dulce canción de antes}, y yo, agotada como estaba por las actividades del día, no pude evitar irme adormilando poco a poco hasta que se me cerraron por completo los ojos y acabé quedando inconsciente.
~ day = day + 1
-> DESPERTAR.cabana

--- (todobien)***Acabé callándome por completo[...], y vi como poco a poco se iba relajando hasta que, siempre sin decir palabra, me pasó un cuenco con lo que parecía ser una especie de pasta confeccionada a partir de alguna fruta local. Al cabo de un rato {desastre:empezó a cantar suavemente|empezó a cantar la misma dulce canción de antes}, y yo, agotada como estaba por las actividades del día, no pude evitar irme adormilando poco a poco hasta que se me cerraron por completo los ojos y acabé quedando inconsciente.
~ day = day + 1
-> DESPERTAR.cabana

*** Le pregunté por algún poblado cercano[...] en un último intento de sacarle alguna información útil, pero, por lo visto, aquello fue la gota que colmó el vaso en mi lista de terribles e incomprensibles ofensas. 
Mi huésped se levantó y, visiblemente alterado, me dirigió la palabra por primera y última vez: "¡Vete, demonio, no tienes poder sobre mí!", profirió, casi como un encantamiento. 
No me quedó más remedio que marcharme por donde había venido y pasar la noche en la selva, profundamente confundida. ¿Qué diantres habría hecho mal? ¿Y por qué me llamaría demonio? -> DESPERTAR.selvacabanaliada

*** (loco)A lo mejor sencillamente estaba loco[...], reflexioné. Eso explicaría por qué vivía aqui solo {buscarvida:en lugar de tener su casa junto al resto de hogueras que había visto des del promontorio}, y por qué reaccionaba de una forma tan extraña a todos mis comentarios. 
-> todobien



*Me acerqué abiertamente[...] a la hoguera, y al cabo de poco escuché una voz humana por primera vez en más de 300 años: estaba cantando. 
{nombremascota:Curiosamente, {pet}, que se había mostrado dócil y despreocupada durante todo el trayecto, ahora se negaba a dar ni un paso más, encogida en el suelo con aspecto asustado. No me quedó más remedio que dejarla atrás, temblorosa, confiando en que seguiría allí a mi regreso.}
El cantante se puso alerta e interrumpió bruscamente su canción al verme aparecer en el círculo de luz de la hoguera. Era un hombre desnudo de unos cuarenta años 
** ... con el pelo largo y trenzado[...] cayéndole por la espalda en un diseño intrincado y meticuloso. ¿Tendría algun significado particular en su cultura?  
** ... de cuerpo atlético y musculoso[...], con manos fuertes y callosas, que en ese momento trenzaban y destrenzaban con nerviosismo unas briznas de hierba, sin dejar de observarme cautelosamente. 
** ... observándome fijamente[...] y en silencio con una mirada a medio camino entre el recelo y la curiosidad. 
-- A sus espaldas tenía una especie de choza rudimentaria, las paredes echas de ramas y barro y el techo de hojas secas entretejidas. Sonriendo tranquilizadoramente... -> contactobien



*(desastre)"¡Vengo en son de paz!", grité[...] desde lejos, decidiendo que lo mejor sería comunicar mis intenciones pacíficas cuanto antes. Sin embargo, mi declaración pareció generar un gran alboroto en el campamento de enfrente, y antes de llegar a la hoguera me salió al paso un hombre desnudo de unos cuarenta años mirándome con recelo y con una piedra en la mano. 
{nombremascota: {pet} empezó a gruñirle de inmediato, y el hombre no vaciló en lanzarle la piedra en el cuello, que acertó de lleno. Ella profirió un lastimoso gemido y salió disparada en dirección a la espesura con el cuello sangrando, con lo que no me quedó más remedio que}
**{nombremascota} ... seguirla selva adentro[...]. La encontré horas más tarde escondida entre unos arbustos azulados, el largo cuello enroscado, y gimoteando lastimeramente. 
"Bueno, pequeñaja..." le murmuré tranquilizadoramente, "parece que nuestro primer contacto no ha ido exactamente como queríamos". 
{pet} pareció tranquilizarse con mi contacto y al fin pude examinarle la herida del cuello, que no parecía muy grave. 

*** Pasamos la noche allí mismo[...], abrazadas la una a la otra, sin que pudiera evitar preguntarme si habría echado a perder mi única oportunidad de establecer contacto con los nativos. 
~ day = day + 1
-> DESPERTAR.selvacabanaliada

**{nombremascota} ... dejarla marchar[...] selva adentro y encararme con aquel hombre desconocido. 

-- ** Me presenté y traté de explicarle[...] que estaba perdida, -> excusas

** Le pregunté cuál era su nombre[...], tratando de establecer algún tipo de familiaridad, 

--- (excusas) <> pero cuanto más cosas decía más nervioso parecía ponerse aquel desconocido. Al cabo de poco recogió otra piedra e hizo ademán de lanzármela, con lo que no me quedó más remedio que huir selva adentro{nombremascota: como había hecho {pet}}.  
Pasé la noche envuelta en unas hojas gigantes azuladas que encontré en algún momento, sintiéndome miserable {nombremascota:por haber perdido a {pet}|por haber fracasado en mi primer intento de comunicación con los nativos. ¿Qué habría hecho mal?} -> DESPERTAR.selvacabanaliada

** Me senté de rodillas[...] e incliné la cabeza en señal de sumisión, como haría con un animal salvaje, sin pronunciar palabra. Aquello pareció relajar al desconocido, que {not nombremascota:soltó la piedra y }se acuclilló enfrente mío a un palmo de distancia.
Al levantar la cabeza, vi que...
*** ... tenía el pelo largo y trenzado[...] cayéndole por la espalda en un diseño intrincado y meticuloso. ¿Tendría algun significado particular en su cultura?  
*** ... tenía el cuerpo atlético y musculoso[...], con manos fuertes y callosas, que en ese momento trenzaban y destrenzaban con nerviosismo unas briznas de hierba sin dejar de observarme recelosamente. 
*** ... estaba observándome fijamente[...] y en silencio, con una mirada a medio camino entre el recelo y la curiosidad. 
Nuestras miradas se cruzaron por un instante: tenía los ojos claros y penetrantes, con unas patas de gallo que, a pesar de la tensión del momento, le daban un aspecto casi sonriente. 
Entonces, tan bruscamente como se había acercado, volvió a levantarse, haciéndome un ademán que interpeté como una invitación a seguirle hacia la hoguera. 

**** Eso mismo hice[...], casi sin atreverme a creer mi buena fortuna, consciente que tras aquel tumultoso primer contacto, era probable que no volviera a tener otra oportunidad de acercarme a los nativos.
Llegamos hasta su campamento, en el que no parecía haber nadie más, que consistía básicamente en una sola construcción rudimentaria al lado del fuego. Era una especie de choza, las paredes echas de ramas y barro, y el techo de hojas secas entretejidas. Sonriendo tranquilizadoramente, -> contactobien

**** Aproveché la oportunidad para escabullirme[...] en dirección a la espesura de la selva; ya había tenido suficientes emociones por un solo día. 
Pasé la noche envuelta en unas gigantes hojas azuladas que encontré en un claro de la selva, sin poder evitar preguntarme si realmente estaba hecha para una misión como aquella{nombremascota:, y preguntándome dónde habría ido a parar {pet}|.}
~ day = day + 1
-> DESPERTAR.selvacabana



*(mascotacabana) {nombremascota} "¡Adelante {pet}, tú primera!"[] susurré con apremio, animando a {pet} a adelantarse hacia la cabaña. Ella pareció entender a la perfección lo que le pedía, pero empezó a emitir unos gemidos atemorizados, negándose a dar ni un paso más. 
->cabananoche



*Me pareció más prudente esperar[...] a la mañana siguiente para hacer contacto con quienquiera que habitase en la cabaña, no queriendo asustarles apareciendo de repente en medio de la noche. 
Tras un vistazo a mi alrededor, encontré un hueco en el trono de un árbol gigante y me cobijé en el interior para dormir a la espera del amanecer{nombremascota:, con el suave pelaje de {pet} haciéndome de almohada}.
~ day = day + 1
->DESPERTAR.selvacabana

- -> END








=refugio
La madera de la vegetación era flexible pero fuerte, y en tres o cuatro horas de recolección y construcción hube terminado mi flamante 

* ... cabaña[...] hecha de troncos y hojas trenzados con lianas. 
~ shelter = "cabaña"

* ... casita en un árbol[...], cuya elevación debería mantenerme a salvo de la mayoría de animales salvajes. 
~ shelter = "casita colgante"

* (subterranea)... base subterránea[...], con un intrincado sistema de túneles y compuertas camufladas completamente invisibles al ojo desnudo. Ni siquiera los habitantes locales deberían ser capaces de detectar mi presencia con semejante escondite. 
~ shelter = "base subterránea"

- {not subterranea: No sabía cuanto tiempo tardaría en establecer contacto con los nativos, pero, mientras tanto, al menos tendría un lugar donde resguardarme de la lluvia.}
Habiendo dedicado la mayor parte del día a la confección de mi {shelter}, decidí... 

*... que ya había trabajado suficiente[...] por hoy. Lo mejor sería recoger algo de comida, irme a dormir temprano, y tratar de localizar a los nativos al día siguiente. -> DESPERTAR.basebien

*... que ya era hora de empezar a explorar[...] los alrededores, y más teniendo en cuenta que todavía quedaban algunas horas de sol.

** Me fijé en un promontorio elevado[...] que no quedaba muy lejos, des del que sin duda tendría una buena vista de los alrededores. Como estaba decidida a aprovechar al máximo mi primer día, me propuse escalar el promontorio costara lo que costara, 
*** [... aunque me tomé la subida con calma...]->calma
*** [... y al poco rato ya estaba corriendo y brincando...]->atope

** Aún no había explorado los alrededores[...] de mi campamento, y consideré que esta debería ser mi mayor prioridad después de llegar a un mundo desconocido. Pasé varias horas deambulando por la selva y familiarizándome con la flora y la fauna locales, hasta que...  

*** ... cayó la noche y regresé[...] a mi refugio cargada de frutas exóticas que había recolectado, y sintiéndome cada vez más en casa en aquel extraño planeta desconocido. -> DESPERTAR.basebien

*** [... encontré un rastro prometedor...] -> rastro

- ->END







=criatura
Tenía el tamaño de un perro grande, con el pelaje del mismo color entre marrón y violáceo que la tierra y la arcilla de los alrededores, y parecía observar mi charco de vómito con profunda curiosidad, su largo cuello ligeramente inclinado y una oreja más levantada que la otra. 

* No parecía peligrosa[...], y en cuanto empecé a rascarle la cabeza comenzó a undular su largo cuello en un gesto de claro placer. Era muy consciente de que mi misión consistía en establecer contacto con los nativos de mi especie,

** ... pero, ¿cómo podía resistirse a aquellos ojitos saltones[...?] que me miraban con adoración? Sí, lo confieso, me pasé la mañana jugando con aquella extraña criatura, y al llegar la tarde éramos tan amigas que la idea de separarnos jamás se me habría cruzado por la cabeza. 

** ... por lo que, muy a mi pesar[...], tuve que dejarle claro a aquella criatura adorable que tenía cosas importantes que hacer. Ella, sin embargo, no pareció darse en absoluto por aludida, y por mucho que lo intenté, después de aquella fatídica caricia en la cabeza ya no volví a conseguir que se despegara de mí en todo el día.
-- -> nombremascota
-- (adoptada)<> Y, como si quisiera responder a mi pregunta, mi nueva compañera empezó a tirar de mi mano como si tratara de mostrarme algo.  

** Dejé que me guiara al interior de la selva[...], preguntándome adónde me estaría llevando aquella criatura alienígena. 
A juzgar por su largo cuello debía debía de ser herbívora, y su docilidad me hizo pensar que probablemente fuera descendiente de algún animal doméstico traído por los primeros colonos, que se habría asilvestrado con el transcurso de los milenios. 
Cuando ya estaba empezando a perder la fe en que mi guía supiera adónde nos dirigíamos, fuimos a dar en una 

***... inmensa laguna[...] de agua cristalina, donde finalmente comprendí la razón por la que mi amiga había insistido tanto en traerme hasta allí. ¡Había miles de criaturas como ella, y también de otras especies, bebiendo agua y bañándose en la laguna! 

----(laguna)**** Mi especialidad no es la biología, pero[...] aún y así me sentí increíblemente afortunada ante aquella oportunidad de observar la fauna de un planeta alienígena antes que nadie. ->laguna

**** Me pasé el resto del día nadando y jugando[...] con {pet} y el resto de su manada sin ninguna preocupación en el mundo. 

**** Me apercaté de la hora por la posición de los soles[...] sobre la laguna. ¡Ya estaba llegando la tarde, y yo seguía en medio de la nada! 
Traté de indicarle a {pet} que teníamos que despedirnos, pero en cuanto vió que me alejaba de la laguna volvió corriendo hasta mí y se puso a trotar alegremente a mi lado en dirección al claro donde había aterrizado por la mañana. 
"¿Parece que no voy a librarme de ti tan fácilmente, eh?" le dije con una sonrisa. "Bueno, pues que sepas que tenemos trabajo que hacer...", añadí, y con esas palabras

***... oscura madriguera[...] que se internaba en las profundidades subterráneas, donde finalmente comprendí la razón por la que mi amiga había insistido tanto en traerme hasta allí. ¡Sus crías la estaban esperando en el interior! 

----(madriguera) **** Mi especialidad no es la biología, pero[...] sospecho que la fauna de Aang debe tener un sistema reproductivo cuanto menos peculiar, porque en el interior de la madriguera me encontré a dos adultas más cuidando de las crías. Las dos fueron a saludar a {pet} con evidente alegría, y, al ver que ella no me tenía ningún miedo, también se dejaron acariciar. -> madriguera

**** Me pasé el resto del día jugando con {pet} y sus crías[...]

**** Cuando salí al exterior a tomar aire, me percaté de la hora[...] por la posición de los soles, cuya luz se filtraba entre la vegetación de la selva.
Traté de indicarle a {pet} que teníamos que despedirnos, pero en cuanto vió que me alejaba de la laguna volvió corriendo hasta mí y se puso a trotar alegremente a mi lado en dirección al claro donde había aterrizado por la mañana. 
"¿Parece que no voy a librarme de ti tan fácilmente, eh?" le dije con una sonrisa. "Bueno, pues que sepas que tenemos trabajo que hacer...", añadí, y con esas palabras


** Sabía que no podía pasarme el día jugando[...] con el primer animalito que me encontrase por muy adorable que fuera, con lo que resolví llevarla conmigo mientras me encargaba de mis tareas. 
Decidida a hacer algo productivo, 

* Traté de ahuyentarla[...] con gritos y aspavientos, y al final conseguí que aquella extraña criatura se alejara del claro donde acababa de materializarme.
Decidida a hacer algo productivo, 
- -> inicio

*  -> inicio 

* hola []

- ->END





===DESPERTAR

=base

En la base y rebentada

-> END


=basebien

En la base con un bonito refugio

-> END



=cimabien

si no ha hecho hoguera y no mascota, campamento husmeado

-> END


=cimamal

se despierta en la cima echa caldo

-> END



=casa

HOLA

-> END



=selva

la despierta alguna cosa rara por la noche

-> END


=selvacabana

normal: la despierta el nativo


-> END


=selvacabanaliada

marxar de la cabana del nativo
si DIA_1.promontorio, anar al poble

-> END



=cabana

duerme en la cabaña del nativo

-> END






